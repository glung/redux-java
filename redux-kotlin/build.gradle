buildscript {
    ext.kotlin_version = '1.0.0-beta-4583'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.3.1"
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: "maven-publish"

sourceCompatibility = 1.7
targetCompatibility = 1.7

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty('bintrayUser')
    key = properties.getProperty('bintrayApiKey')
    pkg {
        repo = BINTRAY_PKG_REPO
        name = BINTRAY_PKG_NAME
        userOrg = user
        licenses = BINTRAY_PKG_LICENSES
        vcsUrl = BINTRAY_PKG_VCS_URL

        version {
            name = BINTRAY_PKG_VERSION
        }
    }
    publications = ['ReduxJava']
}

publishing {
    publications {
        ReduxJava(MavenPublication) {
            from components.java
            groupId ARTIFACT_GROUP_ID
            artifactId 'redux-java'
            version ARTIFACT_VERSION
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testCompile 'junit:junit:4.12'
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
}